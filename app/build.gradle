apply plugin: 'com.android.application'

//Dagger
apply plugin: 'com.neenbedankt.android-apt'


android {
    signingConfigs {
        debug
        release
    }

    dexOptions {
        javaMaxHeapSize "4g" // 2g should be also OK
    }

    def versionPropsFile = file('version.properties')
    def versionBuild

    /*Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.fraunhofer.cese.madcap"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode versionBuild
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    buildTypes {
        release {
//            shrinkResources false
//            minifyEnabled
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    tasks.whenTaskAdded { task ->
        if (task.name == ':app:assembleRelease') {
            autoIncrementBuildNumber()
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations {
    compile.exclude group: "org.apache.httpcomponents", module: "httpclient"
}

dependencies {
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile project(':sensorlisteners')
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.j256.ormlite:ormlite-android:5.0'

    compile 'com.google.android.gms:play-services-auth:10.2.1'
    compile 'com.google.android.gms:play-services-location:10.2.1'
    compile 'com.google.android.gms:play-services-iid:10.2.1'
    compile 'com.google.android.gms:play-services-awareness:10.2.1'

    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-messaging:10.2.1'
    compile 'com.google.firebase:firebase-crash:10.2.1'

    compile 'com.google.http-client:google-http-client-android:1.22.0'
    compile 'com.google.api-client:google-api-client-android:1.22.0'

    compile 'com.android.support:multidex:1.0.1'

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'

    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'javax.inject:javax.inject:1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'

    //Dagger
    compile 'com.google.dagger:dagger:2.5'
    apt 'com.google.dagger:dagger-compiler:2.5'
}

def debugProps = new Properties()
debugProps.load(new FileInputStream(rootProject.file("debug.properties")))
android.signingConfigs.debug.storeFile rootProject.file(debugProps.keyStore)
android.signingConfigs.debug.storePassword debugProps.keyStorePassword
android.signingConfigs.debug.keyAlias debugProps.keyAlias
android.signingConfigs.debug.keyPassword debugProps.keyAliasPassword

def releaseProps = new Properties()
releaseProps.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(releaseProps.keyStore)
android.signingConfigs.release.storePassword releaseProps.keyStorePassword
android.signingConfigs.release.keyAlias releaseProps.keyAlias
android.signingConfigs.release.keyPassword releaseProps.keyAliasPassword

apply plugin: 'com.google.gms.google-services'




