apply plugin: 'com.android.application'

//Dagger
apply plugin: 'com.neenbedankt.android-apt'


android {
    signingConfigs {
        debug
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "org.fraunhofer.cese.madcap"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    
    lintOptions {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'com.google.code.findbugs'
    all*.exclude group: 'javax.inject', module: 'javax.inject'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile files('libs/gson-2.1.funf.jar')
    compile files('libs/funf-0.4.2.jar')
    //compile(name:'pathsense-android-sdk-bundle-release-2.2.1.0', ext:'aar')
    compile(name: 'pathsense-android-sdk-bundle-release-2.3.0.0-beta', ext: 'aar')
    compile(name: 'acra-4.9.0', ext: 'aar')
    compile project(path: ':backend', configuration: 'android-endpoints')

    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.opencsv:opencsv:3.8'
    compile 'com.google.appengine:appengine-endpoints-deps:1.9.27'

    compile 'com.google.android.gms:play-services-auth:9.6.1'
    compile 'com.google.android.gms:play-services-location:9.6.1'
    compile 'com.google.android.gms:play-services-iid:9.6.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.google.firebase:firebase-core:9.6.1'
    compile 'com.google.firebase:firebase-messaging:9.6.1'

    compile 'com.android.support:appcompat-v7:24.2.1'

    //Dagger
    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.jmockit:jmockit:1.19'
    testApt 'com.google.dagger:dagger-compiler:2.5'

    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestApt 'com.google.dagger:dagger-compiler:2.5'
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("debug.properties")))
android.signingConfigs.debug.storeFile rootProject.file(props.keyStore)
android.signingConfigs.debug.storePassword props.keyStorePassword
android.signingConfigs.debug.keyAlias props.keyAlias
android.signingConfigs.debug.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'


