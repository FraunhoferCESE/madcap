apply plugin: 'com.android.application'

//Dagger
//apply plugin: 'com.neenbedankt.android-apt'

// V2: Apply the new Endpoints Framework client plugin
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // V2: Add the new Endpoints Framework plugin dependencies
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:2.0.1'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}
android {
    signingConfigs {
        debug
        release
    }

    dexOptions {
        javaMaxHeapSize "4g" // 2g should be also OK
    }

    def versionPropsFile = file('version.properties')
    def versionBuild

    /*Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    /* Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk */
    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            println versionBuild
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "org.fraunhofer.cese.madcap"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode versionBuild
        versionName "1.0.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    buildTypes {
        release {
//            shrinkResources false
//            minifyEnabled
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    tasks.whenTaskAdded { task ->
        if (task.name == ':app:assembleRelease') {
            autoIncrementBuildNumber()
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations {
    compile.exclude group: "org.apache.httpcomponents", module: "httpclient"
}

dependencies {
//    compile project(path: ':backend', configuration: 'android-backend')
    //compile project(':sensorlisteners')
    compile project(path:':sensorlisteners', configuration:'default')
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // V2: Endpoints Framework v2 migration
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    compile 'com.google.api-client:google-api-client-android:1.23.0'
    compile 'com.google.http-client:google-http-client-android:1.22.0'

    compile 'com.google.firebase:firebase-config:10.2.4'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.google.android.gms:play-services-auth:10.2.4'
    compile 'com.google.android.gms:play-services-location:10.2.4'
    compile 'com.google.android.gms:play-services-iid:10.2.4'
    compile 'com.google.android.gms:play-services-awareness:10.2.4'
    compile 'com.google.firebase:firebase-core:10.2.4'
    compile 'com.google.firebase:firebase-messaging:10.2.4'
    compile 'com.google.firebase:firebase-crash:10.2.4'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'javax.inject:javax.inject:1'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    //Dagger
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'org.greenrobot:eventbus:3.0.0'
}

def debugProps = new Properties()
debugProps.load(new FileInputStream(rootProject.file("debug.properties")))
android.signingConfigs.debug.storeFile rootProject.file(debugProps.keyStore)
android.signingConfigs.debug.storePassword debugProps.keyStorePassword
android.signingConfigs.debug.keyAlias debugProps.keyAlias
android.signingConfigs.debug.keyPassword debugProps.keyAliasPassword

def releaseProps = new Properties()
releaseProps.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(releaseProps.keyStore)
android.signingConfigs.release.storePassword releaseProps.keyStorePassword
android.signingConfigs.release.keyAlias releaseProps.keyAlias
android.signingConfigs.release.keyPassword releaseProps.keyAliasPassword

apply plugin: 'com.google.gms.google-services'




