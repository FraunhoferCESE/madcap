apply plugin: 'com.android.application'

//Dagger
apply plugin: 'com.neenbedankt.android-apt'


android {
    signingConfigs {
        debug
    }

    def versionPropsFile = file('version.properties')
    def versionBuild

    /*Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "org.fraunhofer.cese.madcap"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode versionBuild
        versionName "1.0.0." + versionBuild
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    tasks.whenTaskAdded { task ->
        if (task.name == ':app:assembleRelease') {
            autoIncrementBuildNumber()
        }
//        else if (task.name == ':app:assembleDebug') {
//            autoIncrementBuildNumber()
//        }
    }

//    gradle.taskGraph.whenReady {taskGraph ->
//        if (taskGraph.hasTask(assembleDebug)) {  /* when run debug task */
//            autoIncrementBuildNumber()
//        } else
//        if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
//            autoIncrementBuildNumber()
//        }
//    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'com.google.code.findbugs'
    all*.exclude group: 'javax.inject', module: 'javax.inject'
}

dependencies {
    compile project(':sensorlisteners')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile files('libs/gson-2.1.funf.jar')
    compile files('libs/funf-0.4.2.jar')
    compile project(path: ':backend', configuration: 'android-endpoints')
    //Dagger
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.opencsv:opencsv:3.8'
    compile 'com.google.appengine:appengine-endpoints-deps:1.9.27'
    compile 'com.google.android.gms:play-services-auth:10.0.1'
    compile 'com.google.android.gms:play-services-location:10.0.1'
    compile 'com.google.android.gms:play-services-iid:10.0.1'
    compile 'com.google.android.gms:play-services-awareness:10.0.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.google.dagger:dagger:2.5'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:design:24.2.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.jmockit:jmockit:1.19'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestApt 'com.google.dagger:dagger-compiler:2.5'
    apt 'com.google.dagger:dagger-compiler:2.5'
    testApt 'com.google.dagger:dagger-compiler:2.5'
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("debug.properties")))
android.signingConfigs.debug.storeFile rootProject.file(props.keyStore)
android.signingConfigs.debug.storePassword props.keyStorePassword
android.signingConfigs.debug.keyAlias props.keyAlias
android.signingConfigs.debug.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'




